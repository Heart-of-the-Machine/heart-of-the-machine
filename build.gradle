import com.modrinth.minotaur.dependencies.ModDependency
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.quiltmc.loom'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    // curse publishing is broken until QKL gets uploaded to curse
//    id 'com.matthewprenger.cursegradle'
    id 'com.modrinth.minotaur'
    // use allure because the JUnit report writer is broken
    id 'io.qameta.allure'
    id 'de.undercouch.download'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    maven {
        // For LBA, LMP, and LNS
        name "AlexIIL"
        url "https://alexiil.uk/maven/"
    }
    maven {
        // For AE2
        name "Modmaven"
        url "https://modmaven.dev/"
        // For Gradle 5.1 and above, limit it to just AE2
        content {
            includeGroup 'appeng'
        }
    }
    maven {
        // For Mod Menu & Shapes
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        // For REI
        name "Shedaniel Maven"
        url "https://maven.shedaniel.me/"
    }
    maven {
        // For Canvas and dependencies
        name = "vram-group"
        url = "https://maven.vram.io/"
    }
    maven {
        // For Cotton-Config for Canvas
        name = "Cotton"
        url = "https://server.bbkr.space/artifactory/libs-release/"
    }
    maven {
        // For REI, odds and ends for Canvas
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        // For Sodium, Sodium Extras, and Indium
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:v2")
    }
    modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"

    // Quilt Standard Libraries. This is technically optional, but you probably want it anyway.
    modImplementation "org.quiltmc:qsl:${project.qsl_version}+${project.minecraft_version}"

    // Quilted Fabric API
    modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${project.qfapi_version}-${project.minecraft_version}"

    // Quilt Kotlin Libraries (includes Fabric Language Kotlin)
    modImplementation "org.quiltmc.quilt-kotlin-libraries:quilt-kotlin-libraries:${project.qkl_version}"

    // Mod dependencies

    // LNS
    modImplementation("alexiil.mc.lib:libnetworkstack-base:${project.lns_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "alexiil.mc.lib:libnetworkstack-base:${project.lns_version}"

    // FREX
    modApi("io.vram:frex-fabric-${grondag_mc_tag}:${frex_version}") {
        transitive false
    }
    include "io.vram:frex-fabric-${grondag_mc_tag}:${frex_version}"

    // JMX
    modImplementation("io.vram:jmx-fabric-${grondag_mc_tag}:${jmx_version}") {
        transitive false
    }
    include "io.vram:jmx-fabric-${grondag_mc_tag}:${jmx_version}"

    // Shapes
    modImplementation("com.terraformersmc.terraform-api:terraform-shapes-api-v1:${shapes_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "com.terraformersmc.terraform-api:terraform-shapes-api-v1:${shapes_version}"

    // Non-mod dependencies

    implementation "org.yaml:snakeyaml:${yaml_version}"
    include "org.yaml:snakeyaml:${yaml_version}"

    // Runtime Dependencies

    // Mod Menu
    modRuntimeOnly("com.terraformersmc:modmenu:${modmenu_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // REI
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntimeOnly("dev.architectury:architectury-fabric:${architectury_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Bitkit for some reason, cause FREX needs it
    modRuntimeOnly("io.vram:bitkit:${bitkit_version}")

    // for some annotations that appear in decompiled code

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    // testing frameworks

    testImplementation "io.kotest:kotest-runner-junit5:${project.kotest_version}"
    testImplementation "io.kotest:kotest-assertions-core:${project.kotest_version}"
    testImplementation "io.kotest.extensions:kotest-extensions-allure:${project.kotest_extensions_allure_version}"
    testImplementation "io.mockk:mockk:${project.mockk_version}"
    testImplementation "org.objenesis:objenesis:${project.objenesis_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.version

    exclude("**/*.xcf")

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def javaVersion = JavaVersion.VERSION_17

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

tasks.withType(KotlinCompile) {
    kotlinOptions.jvmTarget = javaVersion.toString()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(javaVersion.toString())

    sourceCompatibility javaVersion
    targetCompatibility javaVersion

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archives_base_name}" }
    }
}

afterEvaluate {
    runClient {
        jvmArgs("-Dmixin.debug=true")
        args("--width", "1280", "--height", "720")
    }
}

allure {
    autoconfigure = false
    version = project.allure_version
}

test {
    useJUnitPlatform()
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

def commaRegex = "\\s*,\\s*"

//if (System.getenv("CURSE_API_KEY") != null) {
//    curseforge {
//        apiKey = System.getenv("CURSE_API_KEY")
//        project {
//            id = '391897'
//            changelogType = 'markdown'
//            changelog = file("changelogs/changelog-${project.mod_version}.md")
//            releaseType = project.mod_release_type
//            project.publish_minecraft_versions.split(commaRegex).toList().forEach {
//                addGameVersion it
//            }
//            addGameVersion 'Java 17'
//            addGameVersion 'Fabric'
//            mainArtifact(remapJar)
//            relations {
//                requiredDependency "fabric-api"
//                requiredDependency "fabric-language-kotlin"
//            }
//        }
//        options {
//            forgeGradleIntegration = false
//        }
//    }
//
//    afterEvaluate {
//        tasks.curseforge391897.dependsOn remapJar
//    }
//}

if (System.getenv("MODRINTH_API_KEY") != null) {
    modrinth {
        token = System.getenv("MODRINTH_API_KEY")
        projectId = '7vleuAJ9'
        versionNumber = project.mod_version
        versionType = project.mod_release_type
        def changelogFile = rootProject.file("changelogs/changelog-v${project.mod_version}.md")
        if (changelogFile.exists()) {
            changelog = changelogFile.text
        }
        uploadFile = remapJar
        additionalFiles = [sourcesJar]
        gameVersions = project.publish_minecraft_versions.split(commaRegex).toList()
        loaders = project.publish_loaders.split(commaRegex).toList()
        dependencies = project.publish_mr_dependencies.split(commaRegex).toList().collect { ModDependency(it, "required") }

        syncBodyFrom = rootProject.file("README.md").text
    }
}
