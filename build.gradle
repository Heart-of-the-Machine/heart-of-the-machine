import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.matthewprenger.cursegradle'
    id 'com.modrinth.minotaur'
    // use allure because the JUnit report writer is broken
    id 'io.qameta.allure'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven { url = "http://maven.fabricmc.net/" }
    maven { name = "BuildCraft"; url = "https://mod-buildcraft.com/maven" }
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        // For Gradle 5.1 and above, limit it to just AE2
        content {
            includeGroup 'appeng'
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    // mc libraries

    modImplementation("alexiil.mc.lib:libblockattributes-all:${project.lba_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    // jij lba
    include "alexiil.mc.lib:libblockattributes-all:${project.lba_version}"

    modImplementation("alexiil.mc.lib:libmultipart-all:${project.lmp_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    // jij lba
    include "alexiil.mc.lib:libmultipart-all:${project.lmp_version}"

    modImplementation("alexiil.mc.lib:libnetworkstack-base:${project.lns_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    // jij lns
    include "alexiil.mc.lib:libnetworkstack-base:${project.lns_version}"

    // normal libraries

    modImplementation "org.yaml:snakeyaml:${yaml_version}"
    include "org.yaml:snakeyaml:${yaml_version}"
    modImplementation "com.terraformersmc:shapes:${shapes_version}"
    include "com.terraformersmc:shapes:${shapes_version}"

    // mod compatibility checks

    modRuntime("io.github.prospector:modmenu:${project.modmenu_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntime("alexiil.mc.mod:simplepipes-all:${project.simplepipes_version}") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "alexiil.mc.lib"
    }
    modRuntime("appeng:appliedenergistics2-fabric:8.1.0-alpha.12") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // for some annotations that appear in decompiled code

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    // testing frameworks

    testImplementation "io.kotest:kotest-runner-junit5:${project.kotest_version}"
    testImplementation "io.kotest:kotest-assertions-core:${project.kotest_version}"
    testImplementation "io.kotest:kotest-extensions-allure:${project.kotest_version}"
    testImplementation "io.mockk:mockk:${project.mockk_version}"
    testImplementation "org.objenesis:objenesis:${project.objenesis_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

allure {
    autoconfigure = false
    version = project.allure_version
}

test {
    useJUnitPlatform()
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

if (System.getenv("CURSE_API_KEY") != null) {
    curseforge {
        apiKey = System.getenv("CURSE_API_KEY")
        project {
            id = '391897'
            changelogType = 'markdown'
            changelog = file("changelogs/changelog-${project.mod_version}.md")
            releaseType = project.mod_release_type
            project.publish_minecraft_versions.split(",").toList().forEach {
                addGameVersion it.trim()
            }
            addGameVersion 'Java 8'
            addGameVersion 'Fabric'
            mainArtifact(remapJar)
            relations {
                requiredDependency "fabric-api"
                requiredDependency "fabric-language-kotlin"
            }
        }
        options {
            forgeGradleIntegration = false
        }
    }

    afterEvaluate {
        tasks.curseforge391897.dependsOn remapJar
    }
}

if (System.getenv("MODRINTH_API_KEY") != null) {
    task publishModrinth(type: TaskModrinthUpload, dependsOn: remapJar) {
        group('Upload')
        description('Publish this mod to Modrinth')

        token = System.getenv("MODRINTH_API_KEY")
        // found using https://api.modrinth.com/api/v1/mod/heart-of-the-machine
        projectId = '7vleuAJ9'
        versionNumber = project.mod_version
        uploadFile = remapJar
        releaseType = project.mod_release_type
        changelog = file("changelogs/changelog-${project.mod_version}.md").text
        project.publish_minecraft_versions.split(",").toList().forEach {
            addGameVersion it.trim()
        }
        addLoader 'fabric'
    }
}

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"
